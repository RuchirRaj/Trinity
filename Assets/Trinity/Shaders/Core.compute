#pragma kernel Update

#include "Common.cginc"
#include "SimplexNoise3D.hlsl"

RWStructuredBuffer<float4> PositionBuffer;
RWStructuredBuffer<float4> NormalBuffer;

CBUFFER_START(Params)
    float Time;
    float Extent;
    float NoiseAmplitude;
    float NoiseFrequency;
    float3 NoiseOffset;
CBUFFER_END

float3 RandomPoint(uint id)
{
    float u = Random(id * 2 + 0) * UNITY_PI * 2;
    float z = Random(id * 2 + 1) * 2 - 1;
    return float3(float2(cos(u), sin(u)) * sqrt(1 - z * z), z);
}

[numthreads(64, 1, 1)]
void Update(uint id : SV_DispatchThreadID)
{
    uint idx1 = id * 3;
    uint idx2 = id * 3 + 1;
    uint idx3 = id * 3 + 2;

    uint seed = floor(Time + id * 0.1) * 10000;
    float3 v1 = RandomPoint(idx1 + seed);
    float3 v2 = RandomPoint(idx2 + seed);
    float3 v3 = RandomPoint(idx3 + seed);

    v2 = normalize(v1 + normalize(v2 - v1) * Extent);
    v3 = normalize(v1 + normalize(v3 - v1) * Extent);

    float l1 = snoise(v1 * NoiseFrequency + NoiseOffset);
    float l2 = snoise(v2 * NoiseFrequency + NoiseOffset);
    float l3 = snoise(v3 * NoiseFrequency + NoiseOffset);

    l1 = abs(l1 * l1 * l1);
    l2 = abs(l2 * l2 * l2);
    l3 = abs(l3 * l3 * l3);

    v1 *= 1 + l1 * NoiseAmplitude;
    v2 *= 1 + l2 * NoiseAmplitude;
    v3 *= 1 + l3 * NoiseAmplitude;

    float3 n = normalize(cross(v2 - v1, v3 - v2));

    PositionBuffer[idx1] = float4(v1, 0);
    PositionBuffer[idx2] = float4(v2, 0);
    PositionBuffer[idx3] = float4(v3, 0);

    NormalBuffer[idx1] = float4(n, 0);
    NormalBuffer[idx2] = float4(n, 0);
    NormalBuffer[idx3] = float4(n, 0);
}
